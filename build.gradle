plugins {
	id 'fabric-loom' version '0.13-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories {
	flatDir { dirs("libraries") }

	maven { url 'https://github.com/not-coded/notcoded-maven/raw/fabric/' }
	maven { url 'https://github.com/ricksouth/serilum-forge-maven/raw/maven/' }
	maven { url "https://jitpack.io" }
	maven { url 'https://maven.nucleoid.xyz' }
	maven { url "https://maven.shedaniel.me/" }
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	include(modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))
	include(modImplementation("net.notcoded:codelib:${project.codelib_version}+${project.minecraft_version}"))
	include(modApi("net.blumbo:blfscheduler:1.0.0-${project.minecraft_version}"))
	modImplementation('xyz.nucleoid:fantasy:0.4.0') {exclude(group: "net.fabricmc.fabric-api") }

	// Optional Inventory Loading option
	modImplementation('com.natamus:saveandloadinventories_fabric:1.16.5-2.4')
	modApi('com.natamus.collective:collective-fabric:1.16.5-5.50')

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

// Custom Manifest and Intermediary mappings declaration
loom {
	setCustomMinecraftManifest("https://gist.githubusercontent.com/rizecookey/4c6142baaccc3875f9b227fe22f2ace5/raw/c8ed74b19f7a5315813c9d4b199798b692a8f359/1.16_combat-6.json")
	setIntermediaryUrl("https://maven.rizecookey.net/net/fabricmc/intermediary/${project.minecraft_version}/intermediary-${project.minecraft_version}-v2.jar")
}

processResources {
	inputs.property "version", project.version
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
}

java {
	withSourcesJar()
}